import QtQuick
import QtQuick.Layouts
import "../theme"
import "../primitives"
import "RoutePolicy.js" as RoutePolicy

KeyRouter {
    id: root

    readonly property string lane: Metrics.lane(width)
    // A rail across the top of a viewport this narrow is a reach rather than
    // a glance, so it moves to the bottom where a thumb already is.
    readonly property bool navBarAtBottom: lane === "compact"

    // The chrome frames the page that is on screen, not the one just asked
    // for. A route change lands on `route` at once, but the page it names
    // incubates asynchronously -- and the login page is evicted every time it
    // leaves, so it is always a cold load. Sizing the bar from `route` moved
    // and resized the outgoing page in the meantime: leaving home, the whole
    // screen jumped up by the height of the bar and grew into the space it
    // left, held that for a frame or two, and only then was replaced. Coming
    // back it jumped the other way. Following the route that is actually
    // showing leaves the outgoing page alone and moves the chrome in the same
    // frame the incoming page appears.
    readonly property string chromeRoute: routeStack.activeRoute.length > 0 ? routeStack.activeRoute : route

    // The shell is the one place that knows how big the window is and what
    // the user asked the interface to be scaled to. It hands both to Metrics
    // so nothing under qml/theme has to reach for a backend singleton.
    //
    // Bindings rather than onWidthChanged handlers: a handler only runs when
    // the value changes *after* the item exists, so until the platform
    // reported real geometry every size came off the 1920x1080 fallback in
    // Metrics. On a phone or a television reporting half that in logical
    // pixels the settled scale is much smaller, so the first frames drew the
    // top-row icons visibly too large and they shrank on the first update. A
    // binding is evaluated for the first frame as well.
    Binding {
        target: Metrics
        property: "viewportWidth"
        value: root.width
        when: root.width > 0
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Metrics
        property: "viewportHeight"
        value: root.height
        when: root.height > 0
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Metrics
        property: "zoomPercent"
        value: Settings.uiScalePercent
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Theme
        property: "accentIndex"
        value: Number(Settings.values["theme/accent"])
        restoreMode: Binding.RestoreNone
    }
    Binding {
        target: Theme
        property: "reducedMotion"
        value: Boolean(Settings.values["theme/reducedMotion"])
        restoreMode: Binding.RestoreNone
    }
    Binding {
        target: Theme
        property: "technicalMetadataMode"
        value: String(Settings.values["theme/technicalMetadata"])
        restoreMode: Binding.RestoreNone
    }
    Binding {
        target: Theme
        property: "sideRailLabels"
        value: String(Settings.values["theme/railLabels"])
        restoreMode: Binding.RestoreNone
    }
    Binding {
        target: Theme
        property: "antialiasedText"
        value: Boolean(Settings.values["theme/antialiasedText"])
        restoreMode: Binding.RestoreNone
    }
    Binding {
        target: Theme
        property: "normalTextRenderType"
        value: Number(Settings.values["theme/renderMode"])
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Metrics
        property: "coarsePointer"
        value: true
        when: Boolean(Platform.touchscreen)
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Metrics
        property: "mobileLayout"
        value: Boolean(Platform.touchscreen)
        restoreMode: Binding.RestoreNone
    }

    // Which kind of pointer last touched the app, watched rather than
    // declared: the same build runs on a television with no pointer at all,
    // a desktop with a mouse, and a phone with neither. Both handlers are
    // passive, so nothing here takes an event away from what is under it.
    PointHandler {
        acceptedDevices: PointerDevice.TouchScreen
        onActiveChanged: if (active) {
                             Metrics.coarsePointer = true
                             Metrics.keyboardFocusActive = false
                             Metrics.pointerActive = false
                         }
    }

    // Android dp and iOS points are distance-corrected logical pixels. Android
    // has already decided what a dp should be for the panel in front of it: a
    // handset reports a few hundred dp across because it is held at arm's
    // length, and a television reports about 960x540 because it is watched
    // from across a room. Scoring that television viewport at 1.0 is what
    // lets both Android form factors start at 100% zoom, instead of a 150%
    // default correcting a desktop-shaped yardstick.
    //
    // sqrt(960 * 540) = 720.
    Binding {
        target: Metrics
        property: "baselinePx"
        value: Platform.isAndroid || Platform.isMobile ? 720 : 1440
        restoreMode: Binding.RestoreNone
    }

    Binding {
        target: Metrics
        property: "pixelsPerMm"
        value: Screen.pixelDensity > 0 ? Screen.pixelDensity : 3.8
        restoreMode: Binding.RestoreNone
    }

    HoverHandler {
        // Android can synthesize mouse hover around a touch sequence. It must
        // not re-enable keyboard focus or resize controls between swipes.
        enabled: !Platform.touchscreen
        acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
        onHoveredChanged: if (hovered) {
                              Metrics.coarsePointer = false
                              Metrics.keyboardFocusActive = true
                              Metrics.pointerActive = true
                          }
    }
    focus: true
    backspaceNavigatesInTextInput: Platform.isTV
    webOsScanCodes: Platform.isTV
    tvOsRemote: Boolean(Platform.isTVOS)
    platformMayPairHolds: Platform.isAndroid && Platform.isTV

    readonly property string route: Router.route
    readonly property var routeArgs: Router.args || ({})
    // Routes that stand outside browsing: no rail, no now-playing chrome.
    readonly property bool setupRoute: chromeRoute === "addProvider" || chromeRoute === "providerScreen"
    readonly property bool signedIn: Providers.hasAccounts || Downloads.jobs.some(job => job.state === "complete")
    onRouteChanged: root.exitArmedAt = 0

    // Back is how an Android app is left, and at the top of the stack there is
    // nowhere further to go. Nothing here ever exited, so the press did
    // nothing at all and the only way out was the launcher. Two presses leave;
    // the first says so, because back is the most-hit key on a remote and one
    // stray press should not close what someone is halfway through.
    //
    // Nowhere else asks for this. A desktop window is closed, not exited, and
    // webOS has its own way home that does not come through here.
    readonly property bool backExitsAtRoot: Platform.isAndroid
    property int exitConfirmWindowMs: 3000
    property double exitArmedAt: 0
    readonly property bool diagnosticsVisible: Boolean(Settings.values["shell/diagnostics"])
    property bool mediaInfoVisible: false
    property bool itemMenuLoaded: false
    property int uiScaleShortcutKey: 0
    property int searchShortcutKey: 0
    property int settingsShortcutKey: 0
    property int openFilesShortcutKey: 0
    readonly property bool desktopFilesAvailable: Platform.hasDesktopPointer && !Platform.isTV
    readonly property var fileDropDialog: fileDropDialogLoader.item
    readonly property var activePage: routeStack.activeItem
    readonly property bool pageInputOwned: Boolean(activePage && (activePage.modalVisible || activePage.choiceVisible
                                                                  || activePage.resetVisible || activePage.editingKey))
    readonly property bool unrelatedFileInputOwned: textInputActive || setupRoute || Providers.startupChoicePending
                                                    || startupSplash.visible || updateDialog.open || tlsTrustPending
                                                    || networkConsentPending || remoteGroupConfirmationPending
                                                    || providerOverlay !== null || Downloads.opened || itemMenuOpen
                                                    || mediaInfoVisible || navBar.menuOpen || pageInputOwned || (
                                                        videoSurface.active && videoSurface.modalInputActive)
    readonly property bool fileInputBlocked: unrelatedFileInputOwned || (providerInstallDialog.visible && (
                                                                             !fileDropDialog || fileDropDialog.phase
                                                                             !== "installing"))
    readonly property bool itemMenuOpen: itemContextMenuLoader.item ? itemContextMenuLoader.item.opened : false
    readonly property bool tlsTrustPending: TlsTrust.pending
    readonly property var networkConsent: Providers.networkConsent || ({})
    readonly property bool networkConsentPending: Boolean(networkConsent.id)
    readonly property bool remoteGroupConfirmationPending: RemoteTargets.groupLeaveConfirmationPending
    readonly property bool remoteControlsVisible: contentLayer.visible && (chromeRoute === "remoteControl"
                                                                           || remoteMini.visible)
    onRemoteControlsVisibleChanged: RemoteTargets.setControlsVisible(remoteControlsVisible)
    property var mediaInfoItem: ({})
    // A provider screen asked for while something plays (a release picker).
    property var providerOverlay: null
    property var downloadsReturnFocus: null
    property bool downloadsDestinationRequested: false
    property var pendingPlaybackBackItem: ({})
    textInputActive: Qt.inputMethod.visible || InputKeys.isTextInputItem(root.Window.window
                                                                         ? root.Window.window.activeFocusItem : null)
    property var navigationTarget: routeStack
    activeTarget: updateDialog.open ? updateDialog : tlsTrustPending ? tlsTrustDialog : networkConsentPending
                                                                       ? networkConsentLoader.item :
                                                                         providerInstallDialog.visible
                                                                         ? providerInstallDialog : fileDropDialog
                                                                           && fileDropDialog.visible ? fileDropDialog :
                                                                                                       remoteGroupConfirmationPending
                                                                                                       ? remoteGroupConfirmationLoader.item :
                                                                                                         providerOverlay
                                                                                                         ? providerOverlayLoader.item :
                                                                                                           Downloads.opened
                                                                                                           ? downloadsDialogLoader.item :
                                                                                                             itemMenuOpen
                                                                                                             ? itemContextMenuLoader.item :
                                                                                                               mediaInfoVisible
                                                                                                               ? mediaInfoOverlayLoader.item :
                                                                                                                 hasPlayer
                                                                                                                 && player.visible
                                                                                                                 ? videoSurface :
                                                                                                                   navigationTarget
    backHandler: function () {
        return root.back()
    }
    globalHandler: function (key, phase, repeat, modifiers) {
        return root.globalShortcut(key, phase, repeat, modifiers)
    }
    readonly property var player: Player
    readonly property bool hasPlayer: true
    readonly property bool playerSessionActive: hasPlayer && player.sessionActive
    // The player owns the screen for a whole queue step, not only while a file
    // is decoding. mpv drops the surface the instant it reports end-of-file and
    // the next item takes most of a second to negotiate, so keying the shell on
    // player.visible alone flashed the details page between every two tracks.
    readonly property bool playerHoldsScreen: hasPlayer && (player.visible || App.playbackTransition)
    readonly property string errorTextValue: App.errorText
    readonly property bool busyValue: App.busy
    readonly property string busyTextValue: App.busyText
    property real keyboardAvoidance: 0

    // The room the system's own furniture takes: the clock and notifications
    // at the top, the gesture bar at the bottom, a cutout at either side.
    // Read from the shell rather than from the layer it insets, because an
    // item that moves itself by its own safe area never settles.
    readonly property bool interfaceSharesScreen: !(root.hasPlayer && root.playerHoldsScreen)
    readonly property real safeTopPx: interfaceSharesScreen ? root.SafeArea.margins.top : 0
    readonly property real safeBottomPx: interfaceSharesScreen ? root.SafeArea.margins.bottom : 0
    readonly property real safeLeftPx: interfaceSharesScreen ? root.SafeArea.margins.left : 0
    readonly property real safeRightPx: interfaceSharesScreen ? root.SafeArea.margins.right : 0

    // Playback takes the whole panel; everything else leaves the system's
    // bars visible and stays clear of them.
    onInterfaceSharesScreenChanged: NativeWindow.setImmersive(!interfaceSharesScreen)

    function refreshKeyboardAvoidance() {
        if (!Qt.inputMethod.visible) {
            keyboardAvoidance = 0
            return
        }
        const window = root.Window.window
        const focusItem = window ? window.activeFocusItem : null
        if (!InputKeys.isTextInputItem(focusItem) || !focusItem.mapToItem) {
            keyboardAvoidance = 0
            return
        }
        const keyboardRect = Qt.inputMethod.keyboardRectangle
        const keyboardTop = keyboardRect && keyboardRect.height > 0 ? keyboardRect.y : root.height
        const focusPos = focusItem.mapToItem(root, 0, 0)
        const focusBottom = focusPos.y + focusItem.height + keyboardAvoidance
        const overlap = focusBottom + Metrics.scaled(24) - keyboardTop
        keyboardAvoidance = Math.max(0, Math.min(overlap, root.height * 0.45))
    }

    Behavior on keyboardAvoidance {
        enabled: !Theme.reducedMotion
        NumberAnimation {
            duration: 120
            easing.type: Easing.OutCubic
        }
    }

    Connections {
        target: Qt.inputMethod
        function onVisibleChanged() {
            root.refreshKeyboardAvoidance()
        }

        function onKeyboardRectangleChanged() {
            root.refreshKeyboardAvoidance()
        }
        function onAnchorRectangleChanged() {
            root.refreshKeyboardAvoidance()
        }
    }

    Connections {
        target: NativeWindow
        function onPointerBackRequested() {
            root.back()
        }
        function onPointerForwardRequested() {
            root.forward()
        }
        function onPlatformSurfaceExposed(exposed) {
            if (exposed)
                root.restoreInputFocus()
        }
    }

    // Coming back from the background can leave the scene with nothing
    // focused: the window is handed the remote's keys again, but the key
    // router they are meant for no longer has active focus, so every press
    // goes nowhere until something else happens to take it. Put focus back
    // where the shell already says input belongs.
    function restoreInputFocus() {
        const window = root.Window.window
        if (textInputActive || (window && window.activeFocusItem))
            return
        // activeTarget is already the shell's answer to where input belongs,
        // dialog or player or page, so it is the right thing to hand back to.
        InputKeys.focus(activeTarget || navigationTarget)
    }

    Connections {
        target: root.Window.window
        function onActiveFocusItemChanged() {
            root.refreshKeyboardAvoidance()
            // Deferred, because a page being swapped out drops focus for the
            // rest of the turn before whatever replaces it takes focus back.
            // restoreInputFocus stands down if that happened.
            if (!root.Window.window.activeFocusItem)
                Qt.callLater(root.restoreInputFocus)
        }
    }

    Connections {
        target: App
        function onInitializedChanged() {
            if (App.initialized)
                root.applyInitializedRoute()
        }
        function onAggressiveMemoryPressure() {
            if (!root.itemMenuOpen)
                root.itemMenuLoaded = false
            routeStack.trim()
        }
        function onToastMessage(message) {
            toast.show(message)
        }
        function onRemoteUiActionRequested(action) {
            root.handleRemoteUiAction(action)
        }
        function onRemoteMessageRequested(message) {
            remoteMessageText.text = message
            remoteMessage.visible = true
            remoteMessageTimer.restart()
        }
        function onRemoteContentRequested(itemId, itemType, title) {
            root.exitPlaybackForRemoteNavigation("remote-content")
            root.pushRoute("itemDetails", {
                               "itemId": itemId,
                               "itemType": itemType || "Video",
                               "title": title || "Selected item",
                               "returnRoute": root.route
                           })
        }
    }

    Connections {
        target: Providers
        function onRestoredChanged() {
            if (App.initialized)
                root.applyInitializedRoute()
        }
        function onAccountSetupStarted(accountId) {
            Router.reset("accounts", {
                             "onboarding": accountId
                         })
            root.navigationTarget = routeStack
            InputKeys.focus(routeStack)
        }
        function onComponentRequested(context) {
            root.providerOverlay = context
        }
    }

    function resolveNetworkConsent(approved) {
        if (networkConsentPending)
            Providers.resolveNetworkConsent(String(networkConsent.id), approved)
        Qt.callLater(function () {
            if (root.activeTarget)
                InputKeys.focus(root.activeTarget)
        })
    }

    Component.onDestruction: {
        if (root.networkConsentPending)
            Providers.resolveNetworkConsent(String(root.networkConsent.id), false)
    }
    function showToastAction(message, actionText, callback) {
        toast.showAction(message, actionText, callback)
    }

    function defaultRoute() {
        if (!root.signedIn)
            return "addProvider"
        return "home"
    }

    function restoreRecoveredRoute() {
        if (!Router.recoveryPending || !root.signedIn)
            return false
        const args = root.routeArgs
        if (root.route === "libraryGrid") {
            const libraryId = String(args.libraryId || "")
            if (libraryId.length <= 0) {
                Router.reset("home")
            } else if (!App.openLibraryById(libraryId)) {
                if (Libraries.count > 0)
                    Router.reset("home")
                else
                    return false
            }
        } else if (root.route === "itemDetails") {
            const itemId = String(args.itemId || "")
            if (itemId.length <= 0) {
                Router.reset("home")
            } else {
                Content.prepareLinkedItem(itemId, String(args.title || "Selected item"), String(args.itemType || "Video"),
                                          String(args.seriesId || ""), String(args.title || ""), String(args.seasonId
                                                                                                        || ""))

                const restored = Object.assign({}, args)
                restored.model = Content.linkedItems
                Router.replace("itemDetails", restored)
            }
        } else if (root.route === "personDetails") {
            const personId = String(args.personId || "")
            if (personId.length <= 0)
                Router.reset("home")
        }
        Router.finishRecovery()
        return true
    }

    function applyInitializedRoute() {
        if (!Providers.restored)
            return
        // A recovered page must never bypass a server's startup viewer choice.
        if (Providers.startupChoicePending) {
            Router.finishRecovery()
            Router.reset("accounts", {
                             "startup": true
                         })
            return
        }
        if (Router.recoveryPending) {
            if (root.restoreRecoveredRoute())
                return
            if (root.signedIn)
                return
        }
        Router.reset(root.defaultRoute())
    }

    // Every platform states its own text rendering rather than inheriting a
    // default, so that tuning one cannot quietly move another.
    //
    // A television draws a 1080p scene that the panel upscales to 4K over an
    // RGBW subpixel layout: there is no subpixel geometry worth rendering for,
    // and anything soft at 1080p is softer again by the time it reaches the
    // screen. It takes the platform rasterizer with full hinting, which is
    // what keeps stems on whole pixels and glyphs crisp through the upscale.
    //
    // Linux native rendering goes through the platform FreeType/fontconfig
    // path, including the user's antialiasing and subpixel policy. Light
    // hinting keeps baselines aligned without snapping stems to whole pixels.
    // The durable render-mode setting owns the text rasterizer.
    Component.onCompleted: {
        // A phone is a finger until something says otherwise. The pointer
        // handlers above only fire once the app has been touched, so without
        // this the first frame is laid out to a mouse's sizes.
        if (!Platform.hasDesktopPointer && !Platform.isTV) {
            Metrics.coarsePointer = true
            Metrics.keyboardFocusActive = false
        }
        if (Platform.isTV) {
            // Full hinting snaps stems to whole pixels, which is what makes
            // the television's text look chiselled at a viewing distance.
            // Hinting the vertical metrics alone keeps the weight and the
            // baseline crisp while curves antialias smoothly.
            Typography.sansHinting = Font.PreferVerticalHinting
        } else if (Qt.platform.os === "linux") {
            Typography.sansHinting = Font.PreferVerticalHinting
        }
        // The TV keeps its two-step text entry: there the field taking focus
        // is what raises the on-screen keyboard, so the row stays the D-pad
        // target until Select is pressed.
        Theme.textEntryFollowsFocus = !Platform.isTV
        if (App.initialized)
            root.applyInitializedRoute()
    }

    Connections {
        target: Libraries
        function onCountChanged() {
            if (Router.recoveryPending && root.route === "libraryGrid")
                root.restoreRecoveredRoute()
        }
    }

    Connections {
        target: root.player
        function onVisibleChanged() {
            if (root.hasPlayer && root.player.visible) {
                root.preparePlaybackBackNavigation(PlayQueue.currentIndex >= 0 ? PlayQueue.get(PlayQueue.currentIndex) :
                                                                                 ({}))
                root.focusPlayerInput()
            } else if (App.playbackTransition) {
                // Stepping to the next item, not leaving playback. Navigating
                // here would load details — item plus similar items — for the
                // track that just ended, behind a player surface the shell is
                // deliberately still holding. Nobody sees it and it costs a
                // round trip per track.
            } else {
                root.finishPlaybackBackNavigation()
                root.navigationTarget = routeStack
                InputKeys.focus(routeStack)
            }
        }
    }

    function focusPlayerInput() {
        videoSurface.focusInput()
    }

    function preparePlaybackBackNavigation(item) {
        if (RoutePolicy.itemIdFor(item).length <= 0) {
            pendingPlaybackBackItem = ({})
            return
        }
        pendingPlaybackBackItem = item
        routeStack.preloadRoute("itemDetails")
    }

    function finishPlaybackBackNavigation() {
        const item = pendingPlaybackBackItem
        pendingPlaybackBackItem = ({})
        const itemId = RoutePolicy.itemIdFor(item)
        if (itemId.length <= 0)
            return false
        const returnRoute = route === "itemDetails" ? String(routeArgs.returnRoute || "home") : route
        return openDetailsRoute({
                                    "model": PlayQueue,
                                    "itemId": itemId,
                                    "itemType": RoutePolicy.itemTypeFor(item),
                                    "source": "playback",
                                    "returnRoute": returnRoute,
                                    "focusIndex": Math.max(0, PlayQueue.currentIndex)
                                })
    }

    function pushRoute(nextRoute, args) {
        Router.push(nextRoute, args || ({}))
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
    }

    function commitDetailsRoute(args, source, focusIndex) {
        if (!args) {
            console.warn("details route ignored: missing item id", source || "", Math.max(0, Number(focusIndex || 0)))
            return false
        }
        if (RoutePolicy.detailsNavigationMode(route, routeArgs, args, source) === "replace") {
            Router.replace("itemDetails", args)
            InputKeys.focus(routeStack)
        } else {
            pushRoute("itemDetails", args)
        }
        return true
    }

    function openDetailsRoute(request) {
        return commitDetailsRoute(RoutePolicy.normalizeDetailsRoute(request, Browse.items, route), request
                                  ? request.source : "", request ? request.focusIndex : 0)
    }

    function openDetailsAt(model, index, source, returnRoute, parent) {
        const nextModel = model || (Browse.items)
        return commitDetailsRoute(RoutePolicy.detailsRouteAt(nextModel, index, source, returnRoute, route, parent), source,
                                  index)
    }

    function openSeriesDetails(seriesId, seriesName, returnRoute) {
        const id = String(seriesId || "")
        if (id.length <= 0)
            return false
        const title = String(seriesName || "Series")
        Content.prepareLinkedItem(id, title, "Series", "", title, "")
        return openDetailsAt(Content.linkedItems, 0, "series-link", returnRoute || route)
    }

    function openSeasonDetails(seriesId, seasonId, seasonName, seriesName, returnRoute) {
        const showId = String(seriesId || "")
        const id = String(seasonId || "")
        if (showId.length <= 0 || id.length <= 0)
            return false
        Content.prepareLinkedItem(id, String(seasonName || "Season"), "Season", showId, String(seriesName || ""), id)
        return openDetailsAt(Content.linkedItems, 0, "season-link", returnRoute || route)
    }

    function replaceRoute(nextRoute, args) {
        Router.replace(nextRoute, args || ({}))
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
    }

    function goHome() {
        if (Providers.startupChoicePending) {
            Router.reset("accounts", {
                             "startup": true
                         })
            navigationTarget = routeStack
            InputKeys.focus(routeStack)
            return
        }
        Router.reset("home")
        App.goHome()
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
    }

    function exitPlaybackForRemoteNavigation(reason) {
        if (!playerSessionActive)
            return
        pendingPlaybackBackItem = ({})
        player.stopWithReason(reason, true)
    }

    function handleRemoteUiAction(action) {
        if (action === "toggle-osd") {
            if (player.visible)
                videoSurface.toggleOsd()
            return
        }
        if (action === "fullscreen") {
            if (!Platform.isTV && !Platform.isAndroid)
                NativeWindow.toggleFullScreen()
            return
        }
        if (action === "context-menu") {
            if (player.visible)
                videoSurface.openPlaybackSettings()
            else
                openContextMenu()
            return
        }
        if (action === "settings") {
            if (player.visible)
                videoSurface.openPlaybackSettings()
            else
                pushRoute("settings")
            return
        }
        if (action === "search") {
            exitPlaybackForRemoteNavigation("remote-search")
            pushRoute("search")
            return
        }
        if (action === "home") {
            exitPlaybackForRemoteNavigation("remote-home")
            goHome()
        }
    }

    function openProviderScreen(context) {
        if (context)
            pushRoute("providerScreen", {
                          "context": context
                      })
    }

    function openCollectionEditor(containerId, title) {
        if (!containerId || !Sources.collectionEditingAvailable(containerId))
            return false
        pushRoute("collectionEditor", {
                      containerId: containerId,
                      title: title
                  })
        return true
    }

    function openFiles() {
        if (!desktopFilesAvailable || !fileDropDialog || fileInputBlocked || fileDropDialog.opened)
            return false
        fileDropDialog.openFiles()
        return true
    }

    function openPlaybackSetting(key) {
        if (textInputActive || updateDialog.open || tlsTrustPending || networkConsentPending
                || providerInstallDialog.visible || remoteGroupConfirmationPending || providerOverlay
                || Downloads.opened || itemMenuOpen || mediaInfoVisible || (fileDropDialog && fileDropDialog.opened)
                || pageInputOwned)
            return false
        return videoSurface.openPlaybackSetting(key)
    }

    function releaseTextInput() {
        let item = root.Window.window ? root.Window.window.activeFocusItem : null
        while (item) {
            if (item.releaseTextInput && item.releaseTextInput())
                return
            item = item.parent
        }
        Qt.inputMethod.hide()
        InputKeys.focus(routeStack)
    }

    function back() {
        if (updateDialog.open)
            return updateDialog.back()
        if (tlsTrustPending)
            return tlsTrustDialog.back()
        if (networkConsentPending) {
            resolveNetworkConsent(false)
            return true
        }
        if (providerInstallDialog.visible)
            return true
        if (fileDropDialog && fileDropDialog.visible)
            return fileDropDialog.back()
        if (remoteGroupConfirmationPending) {
            RemoteTargets.confirmLeaveGroup(false)
            return true
        }
        if (providerOverlay) {
            providerOverlay.close()
            return true
        }
        if (Downloads.opened) {
            if (downloadsDialogLoader.item)
                return downloadsDialogLoader.item.back()
            Downloads.close()
            return true
        }
        if (textInputActive) {
            releaseTextInput()
            return true
        }
        if (navBar.visible && navBar.menuOpen) {
            navBar.back()
            return true
        }
        if (diagnosticsVisible) {
            Settings.setValue("shell/diagnostics", false)
            return true
        }
        if (itemMenuOpen && itemContextMenuLoader.item) {
            itemContextMenuLoader.item.closeMenu()
            return true
        }
        if (mediaInfoVisible) {
            closeMediaInfo()
            return true
        }
        if (root.hasPlayer && root.player.visible) {
            if (root.player.backAllowed) {
                root.preparePlaybackBackNavigation(PlayQueue.currentIndex >= 0 ? PlayQueue.get(PlayQueue.currentIndex) :
                                                                                 ({}))
                root.player.stopWithReason("shell-back-fallback", true)
            }
            return true
        }
        if (routeStack.back())
            return true
        if (route === "itemDetails") {
            Router.pop(String(routeArgs.returnRoute || "libraryGrid"))
            InputKeys.focus(routeStack)
            return true
        }
        if (route === "libraryGrid") {
            goHome()
            return true
        }
        if (route === "home" || (route === "addProvider" && !root.signedIn))
            return backAtRoot()
        if (Router.canPop) {
            Router.pop(route === "personDetails" ? "itemDetails" : "home")
            InputKeys.focus(routeStack)
            return true
        }
        return false
    }

    function backAtRoot() {
        if (!backExitsAtRoot)
            return false
        if (exitArmedAt > 0 && Date.now() - exitArmedAt <= exitConfirmWindowMs) {
            exitArmedAt = 0
            NativeWindow.exitToLauncher()
            return true
        }
        exitArmedAt = Date.now()
        toast.show("Press back again to exit", toast.briefDurationMs)
        return true
    }

    function forward() {
        if (networkConsentPending || tlsTrustPending || textInputActive || (navBar.visible && navBar.menuOpen)
                || diagnosticsVisible || itemMenuOpen || providerOverlay || Downloads.opened || mediaInfoVisible
                || playerSessionActive)
            return true
        if (!Router.canForward)
            return false
        if (!Router.forward())
            return false
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
        return true
    }

    function openContextMenu() {
        if (navigationTarget !== routeStack)
            return false
        return routeStack.longPress()
    }

    function openItemMenu(item, anchorItem, context) {
        itemMenuLoaded = true
        return itemContextMenuLoader.item ? itemContextMenuLoader.item.openForItem(item || ({}), anchorItem || null,
                                                                                   context || ({})) : false
    }

    function openDownloads(itemId, returnFocus, destination) {
        downloadsReturnFocus = returnFocus || navigationTarget
        downloadsDestinationRequested = Boolean(destination)
        Downloads.open(itemId || "")
    }

    Connections {
        target: Downloads
        function onOpenedChanged() {
            if (Downloads.opened) {
                if (!root.downloadsReturnFocus)
                    root.downloadsReturnFocus = root.navigationTarget
            } else {
                const target = root.downloadsReturnFocus
                root.downloadsReturnFocus = null
                root.downloadsDestinationRequested = false
                Qt.callLater(() => InputKeys.focus(root.providerOverlay || root.itemMenuOpen ? root.activeTarget :
                                                                                               target && target.visible
                                                                                               ? target :
                                                                                                 root.activeTarget))
            }
        }
    }

    function openLibraryMenu(library, anchorItem, context) {
        const options = Object.assign({}, context || ({}), {
                                          "library": true
                                      })
        return openItemMenu(library || ({}), anchorItem, options)
    }

    function finishItemMenuOpeningGesture() {
        if (itemContextMenuLoader.item)
            itemContextMenuLoader.item.finishOpeningGesture()
    }

    function restoreFocusAfterItemMenu() {
        if (providerOverlay || Downloads.opened || mediaInfoVisible || diagnosticsVisible || player.visible)
            return
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
    }

    function mediaInfoAvailable(item) {
        const type = String(item && item.itemType || "")
        return Boolean(item && item.movieId && type !== "Series" && type !== "Season")
    }

    function openMediaInfo(item) {
        if (!mediaInfoAvailable(item))
            return false
        mediaInfoItem = item || ({})
        if (mediaInfoItem.movieId)
            Content.loadItemDetail(mediaInfoItem.movieId)
        mediaInfoVisible = true
        return true
    }

    function closeMediaInfo() {
        mediaInfoVisible = false
        mediaInfoItem = ({})
        InputKeys.focus(routeStack)
    }

    function openPerson(person) {
        const personId = String(person && person.id || "")
        if (personId.length <= 0)
            return false
        const args = {
            personId: personId,
            personName: String(person.name || "Person"),
            personRole: String(person.role || ""),
            personType: String(person.type || "Person"),
            personImageTag: String(person.imageTag || "")
        }
        if (route === "personDetails" && String(routeArgs.personId || "") === personId)
            replaceRoute("personDetails", args)
        else
            pushRoute("personDetails", args)
        return true
    }

    function currentMediaItem() {
        const page = routeStack.activeItem
        const item = page && page.currentMediaItem ? page.currentMediaItem() : null
        return item || ({})
    }

    function focusNavBar() {
        if (setupRoute || Providers.startupChoicePending || !navBar.visible)
            return
        const page = routeStack.activeItem
        if (page && page.revealHeader)
            page.revealHeader()
        navigationTarget = navBar
        InputKeys.focus(navBar)
        navBar.focusCurrent()
    }

    function focusContent() {
        if (root.hasPlayer && root.player.visible)
            return
        navigationTarget = routeStack
        InputKeys.focus(routeStack)
    }

    function setUiScale(percent) {
        Settings.setUiScalePercent(Math.max(50, Math.min(180, Math.round(Number(percent || 100) / 5) * 5)))
    }

    function globalShortcut(key, phase, repeat, modifiers) {
        if (phase === "press" && key !== 0)
            Metrics.keyboardFocusActive = true
        if (networkConsentPending)
            return false
        if (phase === "release" && key === uiScaleShortcutKey) {
            uiScaleShortcutKey = 0
            return true
        }
        if (phase === "release" && key === searchShortcutKey) {
            searchShortcutKey = 0
            return true
        }
        if (phase === "release" && key === settingsShortcutKey) {
            settingsShortcutKey = 0
            return true
        }
        if (phase === "release" && key === openFilesShortcutKey) {
            openFilesShortcutKey = 0
            return true
        }
        if (repeat)
            return key === uiScaleShortcutKey || key === searchShortcutKey || key === settingsShortcutKey || key
                    === openFilesShortcutKey

        const control = Boolean(modifiers & Qt.ControlModifier)
        const primaryModifier = Boolean(modifiers & (Qt.ControlModifier | Qt.MetaModifier))
        if (phase === "press" && control && key === Qt.Key_F && chromeRoute === "settings" && activePage
                && activePage.visible) {
            if (activeTarget !== navigationTarget || textInputActive || pageInputOwned || playerSessionActive)
                return false
            const handled = Boolean(activePage.unhandledKey && activePage.unhandledKey(key, phase, repeat, modifiers))
            if (handled)
                searchShortcutKey = key
            return handled
        }
        if (phase === "press" && primaryModifier && key === Qt.Key_O) {
            if (!root.openFiles())
                return false
            openFilesShortcutKey = key
            return true
        }
        const shortcutRoute = key === Qt.Key_F ? "search" : key === Qt.Key_Comma ? "settings" : ""
        if (phase === "press" && primaryModifier && shortcutRoute.length > 0) {
            if (shortcutRoute === "search")
                searchShortcutKey = key
            else
                settingsShortcutKey = key
            if (playerSessionActive)
                return true
            if (route === shortcutRoute) {
                navigationTarget = routeStack
                InputKeys.focus(routeStack)
            } else {
                pushRoute(shortcutRoute)
            }
            return true
        }
        if (phase === "press" && control) {
            let scaleDelta = 0
            if (key === Qt.Key_Plus || key === Qt.Key_Equal)
                scaleDelta = 5
            else if (key === Qt.Key_Minus || key === Qt.Key_Underscore)
                scaleDelta = -5
            if (scaleDelta !== 0 || key === Qt.Key_0) {
                uiScaleShortcutKey = key
                setUiScale(key === Qt.Key_0 ? 100 : Settings.uiScalePercent + scaleDelta)
                return true
            }
        }

        if (textInputActive)
            return false
        // Claim the physical Menu press as well as its release so focus remains
        // stable until the release-triggered context menu opens.
        if (phase === "press" && (key === Qt.Key_M || key === Qt.Key_Menu))
            return true
        if (phase !== "release")
            return false
        if (control && key === Qt.Key_D) {
            Settings.setValue("shell/diagnostics", !diagnosticsVisible)
            return true
        }
        if (key === Qt.Key_Slash) {
            pushRoute("search")
            return true
        }
        if (key === Qt.Key_I) {
            if (mediaInfoVisible)
                closeMediaInfo()
            else
                return openMediaInfo(currentMediaItem())
            return true
        }
        if (key === Qt.Key_M || key === Qt.Key_Menu) {
            openContextMenu()
            return true
        }
        if (key === Qt.Key_H || key === Qt.Key_L)
            return deliver(activeTarget, key === Qt.Key_H ? Qt.Key_Left : Qt.Key_Right, "press", false)
        if (key === Qt.Key_Q && root.playerSessionActive) {
            root.player.stopWithReason("shortcut-q", true)
            return true
        }
        return false
    }

    Item {
        anchors.fill: parent
        z: 1000

        PointHandler {
            acceptedButtons: Qt.LeftButton
            onActiveChanged: if (active) {
                                 if (navBar.remoteMenuOpen && !navBar.containsRemotePoint(root, point.position.x,
                                                                                          point.position.y))
                                     navBar.closeRemoteMenu(false)
                                 if (navBar.groupMenuOpen && !navBar.containsGroupPoint(root, point.position.x,
                                                                                        point.position.y))
                                     navBar.closeGroupMenu(false)
                             }
        }
    }

    // Browsing stays below the z=19 video surface. Capturing the entire shell
    // would also capture (and convert a second time) mpv's linear HDR output.
    HdrUiLayer {
        anchors.fill: parent
        hdrOutput: NativeWindow.hdrOutput
        hdrSdrWhiteNits: NativeWindow.hdrSdrWhiteNits

        Rectangle {
            anchors.fill: parent
            color: Theme.bg
            visible: !(root.hasPlayer && root.playerHoldsScreen)
        }

        Item {
            id: contentLayer
            objectName: "shellContentLayer"
            anchors.fill: parent
            anchors.topMargin: -root.keyboardAvoidance + root.safeTopPx
            anchors.bottomMargin: root.keyboardAvoidance + root.safeBottomPx
            anchors.leftMargin: root.safeLeftPx
            anchors.rightMargin: root.safeRightPx
            visible: App.initialized && !(root.hasPlayer && root.playerHoldsScreen)
            enabled: visible

            TopBar {
                id: navBar
                anchors.rightMargin: openFilesButton.visible ? openFilesButton.width + Metrics.scaled(14) : 0
                objectName: "shellNavigationBar"
                anchors.left: parent.left
                anchors.right: parent.right
                // Keep this as ordinary geometry. Conditional anchor bindings are
                // not ordered when the initial compact lane settles to the real
                // window lane; top and bottom can briefly coexist and leave the
                // bar stretched across the viewport.
                y: root.navBarAtBottom ? Math.max(0, parent.height - height) : 0
                height: root.setupRoute || Providers.startupChoicePending ? 0 : Metrics.topBarHeightPx
                edge: root.navBarAtBottom ? "bottom" : "top"
                visible: !root.setupRoute && !Providers.startupChoicePending
                z: 1
                // Same reason as the height above: the rail marks where you are,
                // not where you are going, so it does not blink its selection off
                // for the frames a page takes to arrive.
                shell: root
                currentRoute: root.chromeRoute
                onActiveFocusChanged: if (activeFocus)
                                          root.navigationTarget = navBar
                onNavigate: r => {
                    if (r === "home")
                        root.goHome()
                    else if (r === "settings")
                        root.pushRoute("settings")
                    else
                        root.pushRoute(r)
                }
                onContentRequested: root.focusContent()
            }

            IconButton {
                id: openFilesButton
                objectName: "shellOpenFiles"
                anchors.right: parent.right
                anchors.rightMargin: Metrics.scaled(14)
                y: navBar.y + (navBar.height - height) / 2
                visible: root.desktopFilesAvailable && navBar.visible && !root.playerHoldsScreen
                enabled: !root.fileInputBlocked && !(root.fileDropDialog && root.fileDropDialog.opened)
                iconName: "folder_open"
                accessibleName: "Open files (Ctrl+O)"
                z: 2
                onClicked: root.openFiles()
                onActiveFocusChanged: if (activeFocus)
                                          root.navigationTarget = openFilesButton
                function activate() {
                    root.openFiles()
                }
                function routeKey(key, phase, repeat) {
                    if (!InputKeys.isDirection(key))
                        return false
                    if (phase === "press") {
                        if (InputKeys.isHorizontal(key))
                            InputKeys.focus(navBar)
                        else
                            root.focusContent()
                    }
                    return true
                }
            }

            RouteStack {
                id: routeStack
                objectName: "shellRouteStack"
                anchors.left: parent.left
                anchors.right: parent.right
                y: root.navBarAtBottom ? 0 : navBar.height
                height: Math.max(0, parent.height - navBar.height - remoteMini.height)
                route: root.route
                shell: root
                startupReady: App.initialized
                focus: !(root.hasPlayer && root.playerHoldsScreen)
                onActiveFocusChanged: if (activeFocus)
                                          root.navigationTarget = routeStack
            }

            RemoteNowPlaying {
                id: remoteMini
                objectName: "shellRemoteNowPlaying"
                anchors.left: parent.left
                anchors.right: parent.right
                y: routeStack.y + routeStack.height
                height: visible ? implicitHeight : 0
                visible: (RemoteTargets.selectedTargetId.length > 0 || RemoteTargets.problem.length > 0)
                         && root.chromeRoute !== "remoteControl" && !root.setupRoute
                shell: root
                onOpenControls: root.pushRoute("remoteControl")
                onActiveFocusChanged: if (activeFocus)
                                          root.navigationTarget = remoteMini
            }
        }
    }

    VideoSurface {
        id: videoSurface
        anchors.fill: parent
        active: root.hasPlayer && root.player.visible
        mediaInfoVisible: root.mediaInfoVisible
        diagnosticsVisible: root.diagnosticsVisible
        onPlaybackBackRequested: item => root.preparePlaybackBackNavigation(item)
        z: 19
    }

    DropArea {
        id: desktopDropArea
        objectName: "shellDesktopDropArea"
        property var enteredUrls: []
        function copyUrls(urls) {
            return Array.from(urls, url => String(url))
        }
        onEntered: drag => {
            // Native MIME data can expire before drop. Own values, never the event wrapper.
            const urls = copyUrls(drag.urls)
            enteredUrls = drag.hasUrls ? urls : []
        }
        onExited: enteredUrls = []
        anchors.fill: parent
        enabled: root.desktopFilesAvailable
        onDropped: drop => {
            // Read the native getter once while this event is alive; do no work until drop.
            const freshUrls = copyUrls(drop.urls)
            const urls = drop.hasUrls ? (freshUrls.length > 0 ? freshUrls : enteredUrls.slice()) : []
            enteredUrls = []
            if (urls.length > 0 && root.fileDropDialog) {
                root.fileDropDialog.submitUrls(urls)
                drop.acceptProposedAction()
            }
        }
    }

    // All of these surfaces already lived above video. Keep their individual
    // z values and declaration order, including splash/toast's shared z=70.
    HdrUiLayer {
        anchors.fill: parent
        hdrOutput: NativeWindow.hdrOutput
        hdrSdrWhiteNits: NativeWindow.hdrSdrWhiteNits
        z: 20

        // The launch screen, held until the first page has something to show.
        //
        // The bar is the page being settled, not merely created: startup should be
        // the system's launch frame, then this same picture, then a home screen
        // that has already been painted. It is deliberately not artwork -- the
        // first row's delegates existing is enough, and waiting for every visible
        // poster would hold a black screen over a usable page.
        Item {
            id: startupSplash

            readonly property bool contentSettled: routeStack.startupSettled
            property bool dismissed: false
            // A start that is taking this long is a server that is not answering,
            // not a slow machine, so say so and offer the way out rather than
            // leaving a still picture up.
            readonly property bool slow: slowStart.triggered && !dismissed

            anchors.fill: parent
            visible: !dismissed
            enabled: visible
            z: 70

            onContentSettledChanged: if (contentSettled)
                                         dismissed = true

            Timer {
                id: slowStart
                property bool triggered: false
                interval: 1000
                running: !startupSplash.dismissed
                onTriggered: triggered = true
            }

            // The overlay covers the shell, so while it is asking a question the
            // one control on it is where a remote has to land.
            onSlowChanged: if (slow)
                               InputKeys.focus(switchServerButton)
            onDismissedChanged: if (dismissed)
                                    InputKeys.focus(routeStack)

            Loader {
                id: splashContent
                anchors.fill: parent
                // The same file the pre-shell frame draws, drawn the same way, so
                // replacing that frame with this one changes nothing on screen.
                // setSource with initial properties, not source + assignment:
                // the values are in place before the component completes, and
                // they come from the singleton rather than the view context,
                // which is what resolves from a component the shell loads.
                Component.onCompleted: setSource("qrc:/startup/SplashContent.qml", {
                                                     "pixelsPerDp": Platform.splashPixelsPerDp,
                                                     "coreWidthDp": Platform.splashCoreWidthDp,
                                                     "coreWidthFraction": Platform.splashCoreWidthFraction,
                                                     "coreAspect": Platform.splashCoreAspect,
                                                     "coreSource": Platform.splashImageUrl
                                                 })
            }

            // Grows out of the launch screen rather than replacing it: the mark
            // stays exactly where it was and the waiting appears underneath.
            ColumnLayout {
                anchors.horizontalCenter: parent.horizontalCenter
                y: (splashContent.item ? splashContent.item.markBottomY : parent.height / 2) + Metrics.scaled(36)
                spacing: Metrics.scaled(22)
                opacity: startupSplash.slow ? 1 : 0
                visible: opacity > 0

                Behavior on opacity {
                    NumberAnimation {
                        duration: Theme.reducedMotion ? 0 : 180
                    }
                }

                BusySpinner {
                    Layout.alignment: Qt.AlignHCenter
                    Layout.preferredWidth: Metrics.scaled(34)
                    Layout.preferredHeight: Metrics.scaled(34)
                    running: startupSplash.slow
                    color: Theme.textMuted
                }

                ActionButton {
                    id: switchServerButton
                    Layout.alignment: Qt.AlignHCenter
                    text: "Accounts"
                    kind: "secondary"
                    onClicked: {
                        startupSplash.dismissed = true
                        Router.reset("accounts")
                    }
                }
            }
        }

        Loader {
            id: busyOverlayLoader
            anchors.fill: parent
            z: 40
            // Stepping to the next item is the one case where the busy state should
            // show over the player: the surface is being held deliberately and
            // would otherwise be a frozen last frame with no sign of progress.
            active: App.playbackTransition || (root.busyValue && !(root.hasPlayer && root.playerHoldsScreen))
            asynchronous: true
            source: active ? "BusyOverlay.qml" : ""

            Binding {
                target: busyOverlayLoader.item
                property: "text"
                value: App.playbackTransition ? "Loading next item…" : root.busyTextValue
                when: busyOverlayLoader.item
            }
        }

        // A provider's own screen over whatever is showing, such as the release
        // picker a provider raises while resolving playback.
        Loader {
            id: providerOverlayLoader
            anchors.fill: parent
            z: 59
            active: root.providerOverlay !== null
            sourceComponent: ProviderSurface {
                context: root.providerOverlay
                overlay: true
                onFinished: {
                    root.providerOverlay = null
                    InputKeys.focus(root.activeTarget)
                }
            }
        }

        Loader {
            id: remoteGroupConfirmationLoader
            anchors.fill: parent
            z: 60
            active: root.remoteGroupConfirmationPending
            sourceComponent: ConfirmationDialog {
                title: "Leave watch together?"
                message: "Controlling another device leaves your current watch-together group. Selecting the device does not transfer or start playback."
                confirmText: "Leave and select device"
                onAccepted: {
                    RemoteTargets.confirmLeaveGroup(true)
                    root.focusContent()
                }
                onDismissed: {
                    RemoteTargets.confirmLeaveGroup(false)
                    root.focusContent()
                }
            }
        }

        ProviderUpdatePrompt {
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.bottom: parent.bottom
            anchors.bottomMargin: root.safeBottomPx + Metrics.scaled(24)
            shown: root.chromeRoute === "home" && !root.playerHoldsScreen && App.initialized
            z: 65
        }

        Loader {
            id: itemContextMenuLoader
            anchors.fill: parent
            z: 58
            active: root.itemMenuLoaded
            sourceComponent: ItemContextMenu {
                shell: root
                onClosed: Qt.callLater(root.restoreFocusAfterItemMenu)
            }
        }

        Loader {
            id: downloadsDialogLoader
            anchors.fill: parent
            z: 58.5
            active: Downloads.opened
            sourceComponent: DownloadsDialog {
                destinationExpanded: root.downloadsDestinationRequested
                inputActive: root.activeTarget === downloadsDialogLoader.item
            }
        }

        Loader {
            id: mediaInfoOverlayLoader
            anchors.fill: parent
            z: 59
            active: root.mediaInfoVisible
            sourceComponent: MediaInfoOverlay {
                visible: root.mediaInfoVisible
                item: visible ? (root.mediaInfoItem && Object.keys(root.mediaInfoItem).length > 0 ? root.mediaInfoItem :
                                                                                                    root.currentMediaItem(
                                                                                                        )) : ({})
                shell: root
                onClosed: root.closeMediaInfo()
            }
        }

        Loader {
            anchors.fill: parent
            z: 61
            active: root.diagnosticsVisible && !(root.hasPlayer && root.playerHoldsScreen)
            sourceComponent: DiagnosticsOverlay {
                route: root.route
                focusedItemId: RoutePolicy.itemIdFor(root.currentMediaItem())
            }
        }
        Rectangle {
            id: remoteMessage
            anchors.horizontalCenter: parent.horizontalCenter
            y: Math.round(parent.height * 0.75 - height / 2)
            width: Math.min(parent.width * 0.72, Metrics.scaled(960))
            height: remoteMessageText.implicitHeight + Metrics.scaled(30)
            visible: false
            radius: Theme.radiusMedium
            color: Theme.bgRaised
            z: 69

            AppText {
                id: remoteMessageText
                anchors.centerIn: parent
                width: Math.max(0, parent.width - Metrics.scaled(32))
                color: Theme.textPrimary
                font.pixelSize: Metrics.bodySizePx + Metrics.scaled(1)
                font.weight: Font.Normal
                wrapMode: Text.Wrap
                horizontalAlignment: Text.AlignHCenter
                verticalAlignment: Text.AlignVCenter
            }

            Timer {
                id: remoteMessageTimer
                interval: 10000
                onTriggered: remoteMessage.visible = false
            }
        }

        ToastLayer {
            id: toast
            anchors.fill: parent
            // Toasts sit against the bottom edge, which is where the gesture bar
            // is.
            anchors.bottomMargin: root.safeBottomPx
            z: 70
        }

        Surface {
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.bottom: parent.bottom
            anchors.bottomMargin: Metrics.scaled(32)
            width: Math.min(parent.width * 0.72, Metrics.scaled(960))
            height: root.errorTextValue.length > 0 ? errorText.implicitHeight + Metrics.scaled(28) : 0
            visible: root.errorTextValue.length > 0
            baseColor: Theme.errorPanel
            z: 80
            AppText {
                id: errorText
                anchors.centerIn: parent
                width: Math.max(0, parent.width - Metrics.scaled(28))
                text: root.errorTextValue
                color: Theme.errorText
                wrapMode: Text.Wrap
                horizontalAlignment: Text.AlignHCenter
                verticalAlignment: Text.AlignVCenter
            }
            MouseArea {
                anchors.fill: parent
                onClicked: App.clearError()
            }
        }
        Loader {
            id: fileDropDialogLoader
            anchors.fill: parent
            z: 90
            active: root.desktopFilesAvailable
            function loadDialog() {
                if (active && status === Loader.Null)
                    setSource(Qt.resolvedUrl("FileDropDialog.qml"), {
                                  "shell": root,
                                  "blocked": Qt.binding(() => root.fileInputBlocked)
                              })
            }
            onActiveChanged: {
                if (active)
                    loadDialog()
                else
                    source = ""
            }
            Component.onCompleted: loadDialog()
        }
        ProviderInstallDialog {
            id: providerInstallDialog
            transfers: Store.transfers
            suspendLocalTransfers: root.unrelatedFileInputOwned
            z: 100
        }

        UpdateDialog {
            id: updateDialog
            updater: Platform.updateController
            z: 260
        }
        TlsTrustDialog {
            id: tlsTrustDialog
            visible: root.tlsTrustPending
            trustController: TlsTrust
            inputKeys: InputKeys
            z: 250
        }
        Loader {
            id: networkConsentLoader
            anchors.fill: parent
            z: 240
            active: root.networkConsentPending
            sourceComponent: ConfirmationDialog {
                title: root.networkConsent.kind === "lan" ? "Search local network?" : "Allow provider connection?"
                message: {
                    const consent = root.networkConsent
                    const owner = String(consent.provider || "") + (consent.account ? " — " + consent.account : "")
                    if (consent.kind === "lan")
                        return owner + " wants to search nearby private networks for servers.\n\n"
                                + "This sends bounded, unauthenticated HTTP requests. It does not sign in "
                                + "or allow authenticated connections to discovered servers."
                    return owner + " wants permission to connect to this exact origin:\n\n" + String(consent.origin
                                                                                                     || "") + "\n\n" + (
                                consent.unencrypted
                                ? "This connection is unencrypted. Other people on the network may be able to read or change its traffic.\n\n" :
                                  "") + "Allow only if you trust this destination."
                }
                confirmText: root.networkConsent.kind === "lan" ? "Search" : "Allow"
                onAccepted: root.resolveNetworkConsent(true)
                onDismissed: root.resolveNetworkConsent(false)
            }
        }
        InputLatencyWarning {
            anchors.fill: parent
            z: 90
            monitor: InputLatency
        }
    }
}
